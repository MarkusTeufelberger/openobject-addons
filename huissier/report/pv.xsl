<?xml version="1.0" encoding="iso-8859-1"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:fo="http://www.w3.org/1999/XSL/Format">

	<xsl:decimal-format name="MyFormat" NaN="" zero-digit="0" decimal-separator="," grouping-separator="."/>
	
	<xsl:template match="/">
		<xsl:apply-templates select="report"/>
	</xsl:template>

	<xsl:template match="report">
		<document>
			<template pageSize="21cm,29.7cm" leftMargin="1.5cm" rightMargin="1.5cm" topMargin="1.5cm" bottomMargin="1.5cm" author="Generated by Tiny ERP">
				<pageTemplate id="main">
					<pageGraphics/>
					<frame x1="1.8cm" y1="1.8cm" width="17.4cm" height="26.6cm"/>
				</pageTemplate>
			</template>
			
			<stylesheet>
				<paraStyle name="name" fontName="Helvetica-Bold" fontSize="16" alignment="center"/>
				<blockTableStyle id="result">
					 <blockValign value="TOP"/>
					 <blockAlignment value="RIGHT" start="-3,0" stop="-1,-1"/>
					 <blockFont name="Helvetica-Oblique" size="10" start="0,0" stop="-1,0"/>
<!--					 
					 <blockBackground colorName="grey" start="0,0" stop="-1,0"/>
					 <blockTextColor colorName="white" start="0,0" stop="-1,0"/>
-->					 
			 		<lineStyle kind="LINEABOVE" colorName="black" start="-3,-1" stop="-1,-1"/>
				</blockTableStyle>
			</stylesheet>
			
			<story>
				<blockTable colWidths="1cm,11cm,1.9cm,1.9cm,1.9cm" style="result">
					<tr>
						<td/>
						<td/>
						<td>Prix/Prijs</td>
						<td>6% TVA</td>
						<td>/ BTW 21%</td>
					</tr>
					<xsl:apply-templates select="object"/>
					<tr>
						<td/>
						<td/>
						<td><xsl:value-of select="format-number(sum(object/price), '#.##0,00', 'MyFormat')"/></td>
						<td><xsl:value-of select="format-number(sum(object/vat6), '#.##0,00', 'MyFormat')"/></td>
						<td><xsl:value-of select="format-number(sum(object/vat21), '#.##0,00', 'MyFormat')"/></td>
					</tr>
				</blockTable>
				<spacer length="0.5cm"/>
				<xsl:apply-templates select="dossier"/>
			</story>
		</document>
	</xsl:template>

	<xsl:template match="dossier">
		<para>
			<xsl:text t="1">Après adjudication et paiement des</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="amount_obj"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">lots ci-dessus, j'ai arrêté et clos le présent procès-verbal à la somme de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="amount_adj_letters"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">à majorer de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="format-number(percent_costs, '#.##0,00', 'MyFormat')"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">pour-cents pour les frais soit la somme de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="amount_costs_letters"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">soit au total la somme de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="amount_total_letters"/>
		</para>
		<xsl:if test="vat_number != ''">
			<spacer length="0.5cm"/>
			<para>
				<xsl:text t="1">Les</xsl:text>
				<xsl:text> </xsl:text>
				<xsl:value-of select="format-number(percent_costs, '#.##0,00', 'MyFormat')"/>
				<xsl:text> </xsl:text>
				<xsl:text t="1">% peuvent se détailler comme suit:</xsl:text>
			</para>
			<xsl:apply-templates select="//vat"/>
			<spacer length="0.5cm"/>
			<para>
				<xsl:text t="1">Le solde des</xsl:text>
				<xsl:text> </xsl:text>
				<xsl:value-of select="format-number(percent_costs, '#.##0,00', 'MyFormat')"/>
				<xsl:text> </xsl:text>
				<xsl:text t="1">% ou</xsl:text>
				<xsl:text> </xsl:text>
				<xsl:value-of select="format-number(amount_costs - sum(//vat/amount_vat), '#.##0,00', 'MyFormat')"/>
				<xsl:text> </xsl:text>
				<xsl:text t="1">euro représente les droits d'enregistrement et les frais. La TVA perçue sera ristournée conformément à la loi, la partie étant inscrite aux services de la TVA sous le n°</xsl:text>
				<xsl:text> </xsl:text>
				<xsl:value-of select="vat_number"/>
				<xsl:text>.</xsl:text>
			</para>
		</xsl:if>
	</xsl:template>

	<xsl:template match="vat">
		<para>
			<xsl:text t="1">une TVA de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="format-number(procent, '#.##0,00', 'MyFormat')"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">% sur les lots n°:</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="lots_numbers"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">soit sur la somme de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="format-number(amount_adj, '#.##0,00', 'MyFormat')"/>
			<xsl:text> </xsl:text>
			<xsl:text t="1">euro soit un montant de TVA de</xsl:text>
			<xsl:text> </xsl:text>
			<xsl:value-of select="format-number(amount_vat, '#.##0,00', 'MyFormat')"/>
			<xsl:text> euro.</xsl:text>
		</para>
	</xsl:template>
	
	<xsl:template match="object">
		<tr>
			<td><para><xsl:value-of select="number"/></para></td>
			<td>
				<para>
					<xsl:value-of select="desc"/>
					<xsl:text> </xsl:text>
					<xsl:text t="1">pour</xsl:text>
					<xsl:text> </xsl:text>
					<xsl:value-of select="price_letters"/>
					<xsl:text> </xsl:text>
					<xsl:text t="1">à</xsl:text>
					<xsl:text> </xsl:text>
					<xsl:value-of select="buyer_name"/>
					<xsl:text> </xsl:text>
					<xsl:value-of select="buyer_address"/>
					<xsl:text> </xsl:text>
					<xsl:value-of select="buyer_zip"/>
					<xsl:text> </xsl:text>
					<xsl:value-of select="buyer_city"/>
				</para>
			</td>
			<td><xsl:value-of select="format-number(price, '#.##0,00', 'MyFormat')"/></td>
			<td><xsl:value-of select="format-number(vat6, '#.##0,00', 'MyFormat')"/></td>
			<td><xsl:value-of select="format-number(vat21, '#.##0,00', 'MyFormat')"/></td>
		</tr>
	</xsl:template>

</xsl:stylesheet>
