<?xml version="1.0" encoding="iso-8859-1"?>

<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:fo="http://www.w3.org/1999/XSL/Format">

	<xsl:decimal-format name="MyFormat" NaN="" zero-digit="0" decimal-separator="," grouping-separator="."/>

	<xsl:template match="/">
		<xsl:apply-templates select="listing"/>
	</xsl:template>

	<xsl:template match="listing">
		<document xmlns:fo="http://www.w3.org/1999/XSL/Format">
			<template leftMargin="2.0cm" rightMargin="2.0cm" topMargin="2.0cm" bottomMargin="2.0cm" title="" author="Generated by Tiny ERP" pageSize="29.7cm,21cm">
				<pageTemplate id="first_page">
					<pageGraphics>
						<setFont name="Helvetica" size="10"/>
						<drawString x="1.0cm" y="19.8cm">MAISON DES HUISSIERS DE JUSTICE DE BRUXELLES S.C.</drawString>
						<drawString x="1.0cm" y="19.3cm">Place Anneessens 21-22</drawString>
						<drawString x="1.0cm" y="18.8cm">1000 BRUXELLES</drawString>

						<setFont name="Helvetica" size="11"/>
						<drawRightString x="28.7cm" y="19.8cm">Bruxelles, le <xsl:value-of select="today"/></drawRightString>
						
						<setFont name="Helvetica-Bold" size="13"/>
						<drawCentredString x="14.85cm" y="17.8cm">STRICTEMENT CONFIDENTIEL</drawCentredString>
					</pageGraphics>

					<frame id="list" x1="1.0cm" y1="1.0cm" width="27.7cm" height="16cm"/>
				</pageTemplate>

				<pageTemplate id="other_pages">
					<pageGraphics>
					</pageGraphics>
					
					<frame id="list" x1="1.0cm" y1="1.0cm" width="27.7cm" height="19cm"/>
				</pageTemplate>
			</template>

			<stylesheet>
				<paraStyle name="small" fontName="Courier" fontSize="12" spaceBefore="0mm" spaceAfter="0mm"/>
				<paraStyle name="verysmall" fontSize="10" fontName="Courier" spaceBefore="0mm" spaceAfter="0mm"/>
				<paraStyle name="smallest" fontSize="8" fontName="Courier" spaceBefore="-1mm" spaceAfter="-1Mm"/>

				<blockTableStyle id="palais">
					<blockValign value="TOP"/>
					<blockAlignment value="LEFT"/>
					<blockAlignment value="RIGHT" start="-1,0" stop="-1,-1"/>
					<blockAlignment value="RIGHT" start="3,0" stop="3,-1"/>
<!--					
					<blockFont name="Helvetica-Bold" size="10"/>
-->					
					<blockFont name="Helvetica-Bold" size="10" start="3,0" stop="3,-1"/>
					<blockTextColor colorName="black"/>
					<lineStyle kind="GRID" thickness="0.5" colorName="black" start="0,0" stop="-1,-1"/>
				</blockTableStyle>
			</stylesheet>

			<story>
				<para>Chers Confrères,</para>
				<para>
					<xsl:text>Voici le relevé des ventes sur saisies pour la période du </xsl:text>  
					<xsl:call-template name="format-date"><xsl:with-param name="date" select="date1"/></xsl:call-template>
					<xsl:text> au </xsl:text>
					<xsl:call-template name="format-date"><xsl:with-param name="date" select="date2"/></xsl:call-template>
					<xsl:text>.</xsl:text>
				</para>
				
				<spacer length="1cm"/>

				<blockTable colWidths="8cm,8.2cm,2.8cm,2.0cm,4cm,2.3cm" style="palais">
					<xsl:apply-templates select="dossier">
						<xsl:sort select="date"/>
					</xsl:apply-templates>
				</blockTable>
			</story>
		</document>
	</xsl:template>
	
	<xsl:template match="dossier">
		<tr>
			<td><para><xsl:value-of select="name"/></para></td>
			<td><para>
				<xsl:value-of select="street"/>
				<xsl:if test="city!=''">
					<xsl:text> à </xsl:text>
					<xsl:value-of select="zip"/>
					<xsl:text> </xsl:text>
					<xsl:value-of select="city"/>
				</xsl:if>
			</para></td>
			<td><para>
				<xsl:choose>
					<xsl:when test="vat!=''">
						<xsl:value-of select="vat"/>
					</xsl:when>
					<xsl:otherwise>
<!--					
						<xsl:call-template name="format-date"><xsl:with-param name="date" select="birthdate"/></xsl:call-template>
-->						
						<xsl:value-of select="birthdate"/>
					</xsl:otherwise>
				</xsl:choose>
			</para></td>
			<td><xsl:value-of select="format-number(adj, '#.##0,00', 'MyFormat')"/></td>
			<td><para><xsl:value-of select="study"/></para></td>
			<td><xsl:call-template name="format-date"><xsl:with-param name="date" select="date"/></xsl:call-template></td>
		</tr>
	</xsl:template>

	<xsl:template name="format-date">
		<xsl:param name="date"/>
		<xsl:variable name="year" select="substring-before($date, '-')"/>
		<xsl:variable name="month" select="substring-before(substring-after($date, '-'), '-')"/>
		<xsl:variable name="day" select="substring-after(substring-after($date, '-'), '-')"/>
		<xsl:value-of select="concat($day, '/', $month, '/', $year)"/>
	</xsl:template>
</xsl:stylesheet>


